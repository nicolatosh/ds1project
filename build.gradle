plugins {
    id 'java'
    id 'application'
}

group 'it.unitn.arpino'
version '1.0-SNAPSHOT'

repositories {
    mavenCentral()
}

def versions = [
        ScalaBinary: "2.13"
]
dependencies {
    implementation platform("com.typesafe.akka:akka-bom_${versions.ScalaBinary}:2.6.16")

    implementation "com.typesafe.akka:akka-actor_${versions.ScalaBinary}"
    testImplementation "com.typesafe.akka:akka-testkit_${versions.ScalaBinary}"

    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.7.0'
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.7.0'
}

test {
    useJUnitPlatform()
}

application {
    mainClass = 'it.unitn.arpino.ds1project.Main'
}

tasks.register('uberJar', Jar) {
    manifest {
        attributes 'Main-Class': 'it.unitn.arpino.ds1project.Main'
    }
    archiveClassifier = 'uber'

    from sourceSets.main.output

    dependsOn configurations.runtimeClasspath
    from {
        configurations.runtimeClasspath.findAll { it.name.endsWith('jar') }.collect { zipTree(it) }
    }
}

run {
    standardInput = System.in
}